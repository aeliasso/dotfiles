# Hello emacs TRAMP
[[ $TERM == "dumb" ]] && unsetopt zle && PS1='$ ' && return

# shortcut to this dotfiles path is $ZSH
export ZSH="$HOME/git/dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS=~/git

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -e ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Enable bash completion for zsh
autoload bashcompinit && bashcompinit

# kworkflow
[ -f /home/antone/.local/lib/kw/bash_autocomplete.sh ] && source /home/antone/.local/lib/kw/bash_autocomplete.sh

# For Tilix
# https://github.com/gnunn1/tilix/wiki/VTE-Configuration-Issue
[ -f /etc/profile.d/vte-2.91.sh ] && source /etc/profile.d/vte-2.91.sh

true
